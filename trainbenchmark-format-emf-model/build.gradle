plugins {
    id "org.xtext.builder" version "1.0.0"
}

apply plugin: 'org.xtext.xtend'
apply from: "${rootDir}/gradle/source-layout-xcore.gradle"

ext {
	xtextVersion = '2.23.0'
}

dependencies {
	compile "org.eclipse.xtext:org.eclipse.xtext.xbase:${xtextVersion}"
	compile 'org.eclipse.emf:org.eclipse.emf.ecore.xcore.lib:+'
	testCompile group: 'junit', name: 'junit', version: junitVersion

	xtextLanguages('org.eclipse.emf:org.eclipse.emf.ecore.xcore:+') {
		exclude group: 'org.antlr', module: 'antlr-runtime'
	}
	compile('org.antlr:antlr-runtime:3.2')
	xtextLanguages 'org.eclipse.emf:org.eclipse.emf.ecore.xcore.lib:+'
	xtextLanguages 'org.eclipse.emf:org.eclipse.emf.codegen.ecore:+'
	xtextLanguages 'org.eclipse.emf:org.eclipse.emf.codegen.ecore.xtext:+'
	xtextLanguages "org.eclipse.xtext:org.eclipse.xtext.ecore:${xtextVersion}"
}

sourceSets {
	main {
		resources {
			exclude '**/*.xcore'
		}
	}
}

xtext {
	version = "${xtextVersion}"
	languages {
		ecore {
			setup = 'org.eclipse.xtext.ecore.EcoreSupport'
		}
		codegen {
			setup = 'org.eclipse.emf.codegen.ecore.xtext.GenModelSupport'
		}
		xcore {
			setup = 'org.eclipse.emf.ecore.xcore.XcoreStandaloneSetup'
			generator.outlet.producesJava = true
		}
	}
}
